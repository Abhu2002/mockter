buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.9.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.2'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Force all subprojects to use the correct Kotlin version
configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('com.android.application') ||
                project.plugins.hasPlugin('com.android.library')) {

            android {
                // Auto-add namespace from AndroidManifest.xml if not set
                if (!project.android.hasProperty('namespace') || project.android.namespace == null) {
                    def manifestFile = project.file('src/main/AndroidManifest.xml')
                    if (manifestFile.exists()) {
                        def manifest = new XmlSlurper(false, false).parse(manifestFile)
                        def packageName = manifest.'@package'.text()
                        if (packageName) {
                            project.android.namespace = packageName
                        }
                    }
                }
            }
        }

        // Force Kotlin version for all plugins
        project.configurations.all {
            resolutionStrategy {
                force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}